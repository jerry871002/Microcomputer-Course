	SPEAKER EQU P0.0 
	TONE1 EQU R0 
	TONE2 EQU R1 
	TEMPO EQU R2 
	TIME EQU R3 
	MCOUNT EQU  R4 
	
	ORG 0000H 
	JMP START 
	ORG 000BH
	JMP INTERRUPT
	ORG 0050H 

START:
	MOV IE, #10000010B
 	MOV TMOD, #1 

;鍵盤掃描
ROW1:
	MOV P1, #07FH
	CALL DELAY
	MOV A, P1
	ANL A, #0FH
	MOV 30H, #0
	CJNE A, #0FH, COL1
ROW2:
	MOV P1, #0BFH
	CALL DELAY
	MOV A, P1
	ANL A, #0FH
	MOV 30H, #4
	CJNE A, #0FH, COL1
ROW3:
	MOV P1, #0DFH
	CALL DELAY
	MOV A, P1
	ANL A, #0FH
	MOV 30H, #8
	CJNE A, #0FH, COL1
	JMP ROW1 
COL1:
	CJNE A, #0EH, COL2
	MOV 31H, #0
	JMP SHOW
COL2:
	CJNE A, #0DH, COL3
	MOV 31H, #1
	JMP SHOW
COL3:
	CJNE A, #0BH, COL4
	MOV 31H, #2
	JMP SHOW
COL4:
	CJNE A, #07H, ROW1
	MOV 31H, #3
SHOW:
	MOV A, 30H
	ADD A, 31H
	MOV MCOUNT, A

BEGIN:
    MOV DPTR, #TONEONE
  	MOV A, MCOUNT 
  	MOVC A, @A+DPTR 
 	MOV TONE1, A 

	MOV DPTR, #TONETWO
 	MOV A, MCOUNT 
 	MOVC A, @A+DPTR 
 	MOV TONE2, A 

	MOV TEMPO,#100

 	MOV TH0, TONE1 
 	MOV TL0, TONE2  

 	ACALL SOUND 

 	JMP ROW1 

SOUND:
	SETB TR0
 	MOV TIME,#2  
SOUND1: 
	CJNE TEMPO,#0,SOUND1
	CLR TR0
	RET

INTERRUPT:
	CLR TF0
	MOV TH0, TONE1
	MOV TL0, TONE2
	CPL SPEAKER
	DJNZ TIME, RETURN
	MOV TIME, #2
	DEC TEMPO
RETURN:
	RETI

DELAY:
	MOV R5, #18H
DELAY1:
	MOV R6, #0F4H
DELAY2:
	DJNZ R6, DELAY2
	DJNZ R5, DELAY1
	RET

TONEONE: 
	DB 241
	DB 241
	DB 242
	DB 243
	DB 244
	DB 244
	DB 245
	DB 246
	DB 246
	DB 247
	DB 247
	DB 248
TONETWO:
	DB 23
	DB 243
	DB 183
	DB 123
	DB 42
	DB 215
	DB 144
	DB 9
	DB 157
	DB 32
	DB 159
	DB 24

END